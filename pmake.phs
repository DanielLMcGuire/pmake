#!/usr/bin/env phasor
using("stdsys", "stdtype", "stdio", "stdfile", "stdstr");

var project = "<project>";
var version = "0.0.0";
var subver;
var installFolder = "out";
var srcFolder = fcd();
var cacheFolder = ".pmake";
var cacheFile = c_fmt("%s/%%s.cache", cacheFolder);
var lockFile = c_fmt("%s/%%s.lock", cacheFolder);
var pid = sys_pid();

if (fexists("project.pmake")) {
    project = freadln("project.pmake", 0); // Line 1 - Name
    version = freadln("project.pmake", 1); // Line 2 - Version
    subver = freadln("project.pmake", 2); // Line 3 - Subversion command
    installFolder = fabsolute(freadln("project.pmake", 3)); // Line 4 - Install folder
    srcFolder = fabsolute(freadln("project.pmake", 4)); // Line 5 - Source folder
    cacheFolder = fabsolute(freadln("project.pmake", 5)); // Line 6 - Cache folder
    cacheFile = c_fmt("%s/%%s.cache", cacheFolder);
    lockFile = c_fmt("%s/%%s.lock", cacheFolder);
} else {
    puts_error("project.pmake file not found, using defaults");
}

fn printHelp(program: string, configured: bool) {
    var presetMsg = "preset_message";
    if (configured) {
        putf("Usage: [runtime] %s [preset] [options]", program);
        presetMsg = "required only if regenerating";
    } else {
        putf("Usage: [runtime] %s <preset> [options]\n", program);
        sys_execute("cmake --list-presets"); 
        presetMsg = "required to generate build files";
    }
    printf(`
Options:
  preset           The CMake preset to use for configuration (%s)
  -h, --help       Show this help message
  -i, --install    Install the built files to the specified prefix (default: out)
  -b, --build      Build the project to the specified output folder (default: .pmake/CMakeBuild)
  -s, --src        Specify the source folder (default: current directory)
  -c, --clean      Clean the build and cache folders before building`, presetMsg);
}

fn configure(srcFolder: string, buildFolder: string, preset: string) -> bool {
    if (sys_execute(c_fmt("cmake -S %s -B %s --preset %s", srcFolder, buildFolder, preset)) == 0) {
        return true;
    }
    return false;
}

fn build(buildFolder: string) -> bool {
    if (sys_execute(c_fmt("ninja -C %s", buildFolder)) == 0) {
        return true;
    }
    return false;
}

fn install(buildFolder: string, prefixFolder: string) -> bool {
    if (sys_execute(c_fmt("cmake --install %s --prefix %s", buildFolder, prefixFolder)) == 0) {
        return true;
    }
    return false;
}

fn writeCache(text: string, cacheName: string) -> bool {
    var fileName = c_fmt(cacheFile, cacheName);
    if (fexists(fileName)) {
        if (!frm(fileName)) {
            puts_error("Failed to delete old cache file");
            return false;
        }
    }
    if (!fwrite(fileName, text)) {
        puts_error("Failed to write cache file");
        return false;
    }
    return true;
}

fn readCache(cacheName: string) -> string {
    var fileName = c_fmt(cacheFile, cacheName);
    if (!fexists(fileName)) {
        return "";
    }
    return fread(fileName);
}

fn writeStatus(text: string) -> bool {
    var fileName = c_fmt(lockFile, project);
    var tmpFile = c_fmt("%s.tmp", fileName);
    var finalText = c_fmt("%s\nPID: %d", text, pid);
    if (fexists(fileName)) {
        if (!frm(fileName)) {
            puts_error("Failed to delete old status file");
            return false;
        }
    }
    if (!fwrite(tmpFile, finalText)) {
        puts_error("Failed to write temp status file");
        return false;
    } else {
        if (!fmv(tmpFile, fileName)) {
            puts_error("Failed to move status file");
            return false;
        }
    }
    return true;
}

fn clearStatus() -> bool {
    var fileName = c_fmt(lockFile, project);
    if (fexists(fileName)) {
        if (!frm(fileName)) {
            puts_error("Failed to delete status file");
            return false;
        }
    }
    return true;
}

fn main() -> int {
    printf("pmake - Portable CMake Wrapper (%s)\n%s %s", project, project, version);
    if (subver) { 
        print("-");
        sys_execute(subver);
    }
    else puts("");
    var preset;
    var doConfigure = false;
    var doBuild = false;
    var doInstall = false;
    var doClean = false;
    var outFolder = ".pmake/CMakeBuild";
    var l_statusFile = c_fmt(lockFile, project);
    var i = 1;

    if (!fexists(cacheFolder)) {
        if (!fmkdir(cacheFolder)) {
            puts_error("Failed to create cache directory");
            return -1;
        }
    }

    if (sys_argc() > 1 && !starts_with(sys_argv(1), "-")) {
        preset = to_string(sys_argv(1));
        i = 2;
        doConfigure = true;
    } else {
        if (fexists(c_fmt(cacheFile, "preset"))) {
            preset = readCache("preset");
            putf("Using cached preset: %s\n", preset);
        } else {
            puts("");
            printHelp(sys_argv(0), false);
            frmdir(cacheFolder, true);
            return -1;
        }
    }

    if (fexists(c_fmt(cacheFile, "install")))
        installFolder = readCache("install");
    if (fexists(c_fmt(cacheFile, "build")))
        outFolder = readCache("build");
    if (fexists(c_fmt(cacheFile, "src")))
        srcFolder = readCache("src");

    while (i < sys_argc()) {
        var arg = to_string(sys_argv(i));

        if (arg == "-s" || arg == "--src") {
            if (i + 1 < sys_argc() && !starts_with(sys_argv(i + 1), "-")) {
                srcFolder = to_string(sys_argv(i + 1));
                i = i + 1;
            } else {
                putf_error("Expected source folder after %s", arg);
                return 1;
            }
            writeCache(fabsolute(srcFolder), "src");
        } else if (arg == "-c" || arg == "--clean") {
            doClean = true;
        } else if (arg == "-i" || arg == "--install") {
            doInstall = true;

            if (i + 1 < sys_argc() && !starts_with(sys_argv(i + 1), "-")) {
                installFolder = to_string(sys_argv(i + 1));
                i = i + 1;
            }

            writeCache(fabsolute(installFolder), "install");
        } else if (arg == "-b" || arg == "--build") {
            doBuild = true;

            if (i + 1 < sys_argc() && !starts_with(sys_argv(i + 1), "-")) {
                outFolder = to_string(sys_argv(i + 1));
                i = i + 1;
            }

            writeCache(fabsolute(outFolder), "build");
        } else if (arg == "-h" || arg == "--help") {
            printHelp(sys_argv(0), true);
            return 0;
        } else if (arg == "-f" || arg == "--force") {
            clearStatus();
            frmdir(outFolder, true);
        } else {
            putf_error("Unknown argument: %s", arg);
            printHelp(sys_argv(0), true);
            return 1;
        }

        i = i + 1;
    }

    if (fexists(l_statusFile)) {
        var pid = to_int(substr(freadln(l_statusFile, "1"), 5));
        if (pid != sys_pid()) {
            putf_error("pmake locked by another process %d", l_statusFile);
            return 2;
        }
    }
    if (!doClean) writeStatus("init");

    if (!doClean) writeCache(preset, "preset");

    if (doClean && fexists(cacheFolder)) {
        if (fexists(c_fmt(cacheFile, "configure") || fexists(outFolder))) {
            doConfigure = false;
            if (!frmdir(outFolder, true)) {
                putf_error("Failed to clean output folder %s", outFolder);
                return 1;
            }
        }
            clearStatus();
        }
        frmdir(cacheFolder, true);
        if (!doBuild && !doInstall) {
            puts("Cleaned build and cache folders");
            return 0;
        }
    }

    if (!doClean && !doConfigure && !doBuild && !doInstall) {
        doBuild = true;
    }

    if (!doClean && !doConfigure && !fexists(outFolder)) 
    { 
        puts("Output missing, configuring...");
        doConfigure = true;
    }

    if (doConfigure || !fexists(c_fmt(cacheFile, "configure"))) {
        writeStatus("config");
    	putf("Configuring %s...", project);
        if (configure(srcFolder, outFolder, preset)) {
            puts_error("Configuration failed");
            return 1;
        }
        writeCache(fabsolute(outFolder), "build");
        writeCache(fabsolute(srcFolder), "src");
        writeCache(preset, "preset");
        writeCache(fabsolute(installFolder), "install");
        writeCache("", "configure");
        writeStatus("idle");
    }

    if (doBuild) {
        writeStatus("build");
        putf("Building %s...", project);
        if (build(outFolder)) {
            puts_error("Build failed");
            return 1;
        }
        writeStatus("idle");
    }

    if (doInstall) {
    writeStatus("install");
	putf("Installing %s...", project);
        if (install(outFolder, installFolder)) {
            puts_error("Installation failed");
            return 1;
        }
        writeStatus("idle");
    }
    return 0;
}

var stat = main();
clearStatus();
shutdown(stat);
